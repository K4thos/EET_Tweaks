/////                                                  \\\\\
///// Initialise installation                          \\\\\
/////                                                  \\\\\

OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~
OUTER_SPRINT slash ~/~
OUTER_SPRINT quote ~"~
OUTER_SPRINT tilde "~"
OUTER_SPRINT tra_path ~%MOD_FOLDER%/lang~

OUTER_SPRINT value ~dummy~

ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
	OUTER_SPRINT os_slash ~\~
	OUTER_SPRINT exe ~.exe~
	OUTER_SPRINT bin ~~
	OUTER_SPRINT WEIDU_EXECUTABLE ~setup-EET_Tweaks.exe~
END ELSE BEGIN //osx, unix
	OUTER_SPRINT os_slash ~/~
	OUTER_SPRINT exe ~~
	OUTER_SPRINT bin ~./~
	OUTER_SPRINT WEIDU_EXECUTABLE ~./setup-EET_Tweaks~
END

<<<<<<<< .../blank.txt
>>>>>>>>

/////                                                  \\\\\
///// HANDLE_CHARSETS                                  \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ASSOCIATIVE_ARRAY charsetsTable BEGIN
	"cs_cz" => "CP1250"
	"de_de" => "CP1252"
	"en_us" => "CP1252"
	"es_es" => "CP1252"
	"fr_fr" => "CP1252"
	"it_it" => "CP1252"
	"ja_jp" => "CP932"
	"ko_kr" => "CP949"
	"pl_pl" => "CP1250"
	//"pt_br" => ""
	"ru_ru" => "CP1251"
	//"tr_tr" => ""
	//"uk_ua" => ""
	"zh_cn" => "CP65001"
END
ACTION_DEFINE_ARRAY charsetsConvertArray BEGIN voices ironman categorization wandcase END
LAF HANDLE_CHARSETS
	INT_VAR
	infer_charset = 0
	STR_VAR
	tra_path = EVAL ~%MOD_FOLDER%/lang~
	charset_table = charsetsTable
	convert_array = charsetsConvertArray
END

/////                                                  \\\\\
///// macros                                           \\\\\
/////                                                  \\\\\

DEFINE_PATCH_MACRO EET_READ_APPEARANCE BEGIN
	PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
		READ_ASCII 0x34 small  // Small portrait
		READ_ASCII 0x3c medium // Medium portrait
		READ_BYTE 0x2c metal   // Metal color
		READ_BYTE 0x2d minor   // Minor color
		READ_BYTE 0x2e major   // Major color
		READ_BYTE 0x2f skin    // Skin color
		READ_BYTE 0x30 leather // Leather color
		READ_BYTE 0x31 armor   // Armor color
		READ_BYTE 0x32 hair    // Hair color
	END ELSE BEGIN
		PATCH_FAIL ~%SOURCE_FILESPEC% has unexpected size (%SOURCE_SIZE%). Files can't be patched using this file as a base.~
	END
END

DEFINE_ACTION_MACRO EET_WRITE_APPEARANCE BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.CRE~) BEGIN
		COPY_EXISTING ~%file%.CRE~ ~override~
			PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
				WRITE_ASCIIE 0x34 "%small%" #8  // Small portrait
				WRITE_ASCIIE 0x3c "%medium%" #8 // Medium portrait
				WRITE_BYTE 0x2c "%metal%"       // Metal color
				WRITE_BYTE 0x2d "%minor%"       // Minor color
				WRITE_BYTE 0x2e "%major%"       // Major color
				WRITE_BYTE 0x2f "%skin%"        // Skin color
				WRITE_BYTE 0x30 "%leather%"     // Leather color
				WRITE_BYTE 0x31 "%armor%"       // Armor color
				WRITE_BYTE 0x32 "%hair%"        // Hair color
			END
		BUT_ONLY
	END
END

/////                                                  \\\\\
///// functions                                        \\\\\
/////                                                  \\\\\

DEFINE_PATCH_FUNCTION EET_UPDATE_PORTRAIT
INT_VAR
  is_large = 0
STR_VAR
  resref = ~~
BEGIN
  SET is_large = (is_large != 0)  // normalize to 0 or 1
  GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
  PHP_EACH ab_array AS _ => ab_off BEGIN
    GET_OFFSET_ARRAY2 fx_array "%ab_off%" SPL_V10_HEAD_EFFECTS
    PHP_EACH fx_array AS _ => fx_off BEGIN
      READ_SHORT (fx_off + 0x00) opcode
      PATCH_IF (opcode == 107) BEGIN  // Change portrait
        READ_LONG (fx_off + 0x08) param2
        PATCH_IF (param2 == is_large) BEGIN
          WRITE_ASCIIE (fx_off + 0x14) ~%resref%~ (8)
        END
      END
    END
  END
END
